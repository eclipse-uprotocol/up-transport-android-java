/*
 * Copyright (c) 2024 General Motors GTO LLC.
 *
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License v. 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-FileType: DOCUMENTATION
 * SPDX-FileCopyrightText: 2023 General Motors GTO LLC
 * SPDX-License-Identifier: Apache-2.0
 */
plugins {
    alias libs.plugins.android.library
    id 'maven-publish'
}

apply from: '../gradle/config.gradle'
apply from: '../gradle/jacoco.gradle'

android {
    compileSdk        config.sdkVersion
    compileSdkVersion config.sdkVersion
    buildToolsVersion config.toolsVersion
    namespace         config.namespace
    buildFeatures.buildConfig true

    defaultConfig {
        minSdk config.minSdkVersion
        targetSdk config.sdkVersion
        buildConfigField 'String', 'VERSION_NAME', /"${config.version}"/
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        consumerProguardFiles 'consumer-rules.pro'
    }

    signingConfigs {
        platform {
            storeFile file('certs/platform.keystore')
            storePassword 'android'
            keyAlias 'platform'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.platform
            debuggable true
            minifyEnabled false
        }
        release {
            signingConfig signingConfigs.platform
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
        }
    }

    buildFeatures {
        aidl = true
    }

    compileOptions {
        sourceCompatibility config.jvm.version.java
        targetCompatibility config.jvm.version.java
    }

    testNamespace "${config.namespace}.test"
    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.all {
            testLogging {
                exceptionFormat 'full'
                events 'passed', 'skipped', 'failed'
                outputs.upToDateWhen { false }
                showStandardStreams = true
                jacoco {
                    includeNoLocationClasses = true
                    excludes = ['jdk.internal.*']
                }
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation libs.androidx.appcompat
    implementation libs.up.java

    androidTestImplementation libs.androidx.espresso
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.junit
    androidTestImplementation libs.mockito.android

    testImplementation project(path: ':library')
    testImplementation libs.androidx.junit
    testImplementation libs.junit
    testImplementation libs.mockito.core
    testImplementation libs.mockito.inline
    testImplementation libs.robolectric
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = config.group
                artifactId = config.artifact
                version = config.version

                from components.release
            }
        }
    }
}
